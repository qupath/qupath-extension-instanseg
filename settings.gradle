pluginManagement {
    plugins {
        // Gradle is awkward about declaring versions for plugins
        // Specifying it here, rather than build.gradle, makes it possible
        // to include the extension as a subproject of QuPath itself
        // (which is useful during development)
        id 'org.bytedeco.gradle-javacpp-platform' version '1.5.9'
    }
}

rootProject.name = 'qupath-extension-instanseg'
gradle.ext.qupathVersion = "0.5.1"

dependencyResolutionManagement {

    // Access QuPath's version catalog for dependency versions
    versionCatalogs {
        libs {
            from("io.github.qupath:qupath-catalog:${gradle.ext.qupathVersion}")
        }
    }

    repositories {

        mavenCentral()

        // Add scijava - which is where QuPath's jars are hosted
        maven {
            url "https://maven.scijava.org/content/repositories/releases"
        }

        maven {
            url "https://maven.scijava.org/content/repositories/snapshots"
        }

    }
}
